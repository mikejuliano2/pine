//@version=4
study("MJ - M Candles", overlay=true)

//////////////////////////////////////////////////////////
// 
// === INPUTS ===
// 
//////////////////////////////////////////////////////////

groupTime = 'Time'
groupColors = 'Colors'
groupStyle = 'Style'

useAutoResolution = input(true, 'Use Auto Resolution?', group=groupTime, tooltip='Auto-adjust the higher timeframe resolution based on the current timeframe')
autoResolutionFor1MinTF = input('60', 'Auto Resolution - 1m', group=groupTime, options=['15', '30', '60', '120', '240'], tooltip='Only applicable if on 1m Timeframe and auto-resolution is on')
fixedResolution = input("60", 'Fixed Resolution', type=input.resolution, group=groupTime, tooltip='Only Relevant if auto-resolution is off')
maxTimeframe = input(60, 'Max Timeframe', group=groupTime, minval=1, maxval=240)

usePvsraColor = input(true, 'Use PVSRA Candle Color', group=groupStyle)
showBorder = input(true, 'Show Border', group=groupStyle)
fillOpacity = input(70, 'Opacity - Fill', minval=0, maxval=100, group=groupStyle)
borderOpacity = input(100, 'Opacity - Border', minval=0, maxval=100, group=groupStyle)
wickWidthInBars = input(2.0, title="Wick width in bars", step=0.1, group=groupStyle)

// COLORS

climaxVolBullColor = input(color.lime, 'Climax Bull Color', group=groupColors)
climaxVolBearColor = input(color.red, 'Climax Bear Color', group=groupColors)
aboveAvgVolBullColor = input(color.blue, 'Above Average Volume Bull Color', group=groupColors)
aboveAvgVolBearColor = input(color.fuchsia, 'Above Average Volume Bear Color', group=groupColors)
standardVolBullColor = input(#999999, 'Standard Volume Bull Color', group=groupColors)
standardVolBearColor = input(#4d4d4d, 'Standard Volume Bear Color', group=groupColors)

//////////////////////////////////////////////////////////
// 
// === CALCULATIONS ===
// 
//////////////////////////////////////////////////////////

wickWidth = wickWidthInBars * timeframe.multiplier /  2
intervalValue = timeframe.isintraday ? timeframe.multiplier : timeframe.multiplier * 1440

_getAutoResolution(_interval) => 
    _interval == 1 
      ? autoResolutionFor1MinTF 
      : _interval <= 5  
         ? '60' 
         : _interval <= 60 
          ? '240' 
          : '1D'

_resolution = useAutoResolution ? _getAutoResolution(intervalValue) : fixedResolution     
_interval = security(syminfo.tickerid, _resolution, intervalValue)

_security(expression) => 
     security(syminfo.tickerid, _resolution, expression, barmerge.gaps_off, barmerge.lookahead_on)

_open = _security(open)
_close = _security(close)
_high = _security(high)
_low = _security(low)
_vol = _security(volume)

isValidTimeframe = not timeframe.isseconds and timeframe.isintraday and timeframe.multiplier <= maxTimeframe

minuteOfDay = (hour * 60) + minute
midpoint = floor(_interval / 2)

_isWickTime() => 
    _remainder = minuteOfDay % _interval
    wickCutoffHigh = midpoint + wickWidth
    wickCutoffLow = midpoint - wickWidth
    isWickTime = _remainder > wickCutoffLow and _remainder <= wickCutoffHigh


isWickTime = _isWickTime()
isBullish = _close >= _open

bodyMax = isBullish ? _close : _open
bodyMin = isBullish ? _open : _close

bodyTopBorder = isWickTime ? na : bodyMax
bodyBottomBorder = isWickTime ? na : bodyMin
wickTopBorder = isWickTime ? _high : na
wickBottomBorder = isWickTime ? _low : na

topBorder = isWickTime ? wickTopBorder : bodyTopBorder
bottomBorder = isWickTime ? wickBottomBorder : bodyBottomBorder
borderPlotStyle = plot.style_stepline

//////////////////////////////////////////////////////////
// 
// === PVSRA Color ===
// 
//////////////////////////////////////////////////////////

_getBarColorByVolWeight(volWeight, _open, _close) => 
    if _close > _open
        volWeight == 1 
          ? climaxVolBullColor 
          : volWeight == 2 
              ? aboveAvgVolBullColor 
              : standardVolBullColor
    else
        volWeight == 1 
          ? climaxVolBearColor 
          : volWeight == 2 
              ? aboveAvgVolBearColor 
              : standardVolBearColor

_volWeightedPriceSpread() => volume * (high - low)
_highestVolWeightedPriceSpread() => highest(_volWeightedPriceSpread(), 10)

_getVolumeWeight(_open, _high, _low, _close, _vol) => 
    // The math below matches MT4 PVSRA indicator source
    avgVol = _security(sma(volume, 10)) // average volume from last 10 candles
    volWeightedPriceSpread = _security(_volWeightedPriceSpread()) //climax volume on the previous candle
    highestVolWeightedPriceSpread = _security(_highestVolWeightedPriceSpread()) // highest climax volume of the last 10 candles

    isClimaxVolume = _vol >= (avgVol * 2) or volWeightedPriceSpread >= highestVolWeightedPriceSpread
    isAboveAvgVolume = _vol >= avgVol * 1.5 and not isClimaxVolume
    volWeight = isClimaxVolume ? 1 : isAboveAvgVolume ? 2 : 0

_getColor(_open, _high, _low, _close, _vol) =>
    if usePvsraColor
        volWeight = _getVolumeWeight(_open, _high, _low, _close, _vol)
        barColor = _getBarColorByVolWeight(volWeight, _open, _close)
    else
        isBullish ? climaxVolBullColor : climaxVolBearColor

//////////////////////////////////////////////////////////
// 
// === PLOT ===
// 
//////////////////////////////////////////////////////////

_color = _getColor(_open, _high, _low, _close, _vol)
borderColor = color.new(_color, borderOpacity)
fillColor = color.new(_color, fillOpacity)
transparentColor = color.new(color.white, 100)

topBorderPlot = plot(
      isValidTimeframe ? topBorder : na, 
      color=showBorder ? borderColor : transparentColor,
      title="Top Border", 
      style=borderPlotStyle
      )

bottomBorderPlot = plot(
      isValidTimeframe ? bottomBorder : na, 
      color=showBorder ? borderColor : transparentColor,
      title="Bottom Border", 
      style=borderPlotStyle
      )      

fill(topBorderPlot, bottomBorderPlot, fillColor)

// --------------------------
// Fill the wick
// --------------------------

// hiddenPlotStyle = plot.style_circles

// wickTopHiddenPlot = plot(
//       isValidTimeframe ? wickTopBorder : na, 
//       title="Wick Top Hidden", 
//       color=transparentColor, 
//       style=hiddenPlotStyle
//       )

// wickBottomHiddenPlot = plot(
//       isValidTimeframe ? wickBottomBorder : na, 
//       title="Wick Bottom Hidden", 
//       color=transparentColor, 
//       style=hiddenPlotStyle
//       )      

// fill(wickTopHiddenPlot, wickBottomHiddenPlot, fillColor)

// --------------------------------------------
// Fill the body (that is not also the wick)
// --------------------------------------------

// bodyTopHiddenPlot = plot(
//       isValidTimeframe ? bodyTopBorder : na, 
//       title="Body Top Hidden", 
//       color=transparentColor, 
//       style=hiddenPlotStyle
//       )

// bodyBottomHiddenPlot = plot(
//       isValidTimeframe ? bodyBottomBorder : na, 
//       title="Body Bottom Hidden", 
//       color=transparentColor, 
//       style=hiddenPlotStyle
//       )      

// fill(bodyTopHiddenPlot, bodyBottomHiddenPlot, fillColor)