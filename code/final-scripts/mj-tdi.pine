//@version=4
// Modified by Jakub  a Babo

study("MJ - TDI", shorttitle="MJ - TDI")

rsiPeriod = input(21, minval = 1, title = "RSI Period")
bandLength = input(34, minval = 1, title = "Band Length")
fastMaLength = input(7, minval = 0, title = "Fast MA on RSI")
slowMaLength = input(2, minval = 1, title = "Slow MA on RSI")

bandsColor = color.new(#2962ff, 40)
bandsLineWidth = 2
marketBaseColor = color.new(#ffb74d, 50)
marketBaseLineWidth = 2
marketSignalColor = color.new(#ff5252, 20)
marketSignalLineWidth = 2
rsiColor = color.new(#006064, 0)
rsiLineWidth = 2

lbR = input(title="Pivot Lookback Right", defval=5)
lbL = input(title="Pivot Lookback Left", defval=5)
rangeUpper = input(title="Max of Lookback Range", defval=60)
rangeLower = input(title="Min of Lookback Range", defval=5)
plotBull = input(title="Plot Bullish", defval=true)
plotHiddenBull = input(title="Plot Hidden Bullish", defval=false)
plotBear = input(title="Plot Bearish", defval=true)
plotHiddenBear = input(title="Plot Hidden Bearish", defval=false)
bearColor = color.new(#f21616, 0)
bullColor = color.new(#1cd206, 0)
hiddenBullColor = color.new(color.green, 80)
hiddenBearColor = color.new(color.red, 80)
textColor = color.new(color.black, 0)
divergenceLineWidth = 4
divergenceLineTransparency = 0

coloredBgWhenOutsideBands = input(true, title='Colored BG when outside bands?', group='Background Color')
bgColorTransparency = input(94, title='BG color transparency', group='Background Color', minval=0, maxval=100)


// hline(50, title="Middle Line", linestyle=hline.style_dotted)
// obLevel = hline(70, title="Overbought", linestyle=hline.style_dotted)
// osLevel = hline(30, title="Oversold", linestyle=hline.style_dotted)
// fill(obLevel, osLevel, title="Background", color=color.new(#9915FF, 2))


//////////////////////////////////////////////////////
// 
// --- CALCULATIONS ---
// 
//////////////////////////////////////////////////////

src = close
rsiOfClose = rsi(src, rsiPeriod) // RSI of Close
slowMA = sma(rsiOfClose, slowMaLength) // Moving Average of RSI 2 bars back
rsiMa = sma(rsiOfClose, bandLength) // Moving Average of RSI [current]

offset = (1.6185 * stdev(rsiOfClose, bandLength))

upperBand = rsiMa + offset
lowerBand = rsiMa - offset
marketBase = (upperBand + lowerBand) / 2 // Average of Upper and Lower Bands
marketSignal = sma(rsiOfClose, fastMaLength) // Moving Average of RSI 7 bars back
_rsi = slowMA


//////////////////////////////////////////////////////
// 
// --- PLOT ---
// 
//////////////////////////////////////////////////////

bandUpperPlot = plot(upperBand, "Upper Band", color=bandsColor, linewidth=bandsLineWidth)              
bandLowerPlot = plot(lowerBand, "Lower Band", color=bandsColor, linewidth=bandsLineWidth)              
marketBasePlot = plot(marketBase, "Middle of Bands", color=marketBaseColor, linewidth=marketBaseLineWidth)
rsiLine = plot(slowMA, "Slow MA", color=rsiColor, linewidth=rsiLineWidth)
marketSignalLine = plot(marketSignal, "Fast MA", color=marketSignalColor, linewidth=marketSignalLineWidth)  


//////////////////////////////////////////////////////
// 
// --- DIVERGENCE ---
// 
//////////////////////////////////////////////////////

pivotLowFound = na(pivotlow(_rsi, lbL, lbR)) ? false : true
pivotHighFound = na(pivothigh(_rsi, lbL, lbR)) ? false : true

_inRange(cond) =>
	bars = barssince(cond == true)
	rangeLower <= bars and bars <= rangeUpper

oscHadHigherLow = _rsi[lbR] > valuewhen(pivotLowFound, _rsi[lbR], 1) and _inRange(pivotLowFound[1]) // Osc: Higher Low
oscHadLowerLow = _rsi[lbR] < valuewhen(pivotLowFound, _rsi[lbR], 1) and _inRange(pivotLowFound[1]) // Osc: Lower Low
oscHadLowerHigh = _rsi[lbR] < valuewhen(pivotHighFound, _rsi[lbR], 1) and _inRange(pivotHighFound[1]) // Osc: Lower High
oscHadHigherHigh = _rsi[lbR] > valuewhen(pivotHighFound, _rsi[lbR], 1) and _inRange(pivotHighFound[1]) // Osc: Higher High

priceHadLowerLow = low[lbR] < valuewhen(pivotLowFound, low[lbR], 1) // Price: Lower Low
priceHadHigherLow = low[lbR] > valuewhen(pivotLowFound, low[lbR], 1) // Price: Higher Low
priceHadHigherHigh = high[lbR] > valuewhen(pivotHighFound, high[lbR], 1) // Price: Higher High
priceHadLowerHigh = high[lbR] < valuewhen(pivotHighFound, high[lbR], 1) // Price: Lower High

hasBullishDiv = plotBull and priceHadLowerLow and oscHadHigherLow and pivotLowFound
hasHiddenBullishDiv = plotHiddenBull and priceHadHigherLow and oscHadLowerLow and pivotLowFound
hasBearishDiv = plotBear and priceHadHigherHigh and oscHadLowerHigh and pivotHighFound
hasHiddenBearishDiv = plotHiddenBear and priceHadLowerHigh and oscHadHigherHigh and pivotHighFound

//-----------------------
// Regular Bullish
//-----------------------

plot(
     pivotLowFound ? _rsi[lbR] : na,
     offset=-lbR,
     title="Regular Bullish",
     linewidth=divergenceLineWidth,
     color=hasBullishDiv ? color.new(bullColor, divergenceLineTransparency) : na
     )

plotshape(
	 hasBullishDiv ? _rsi[lbR] : na,
	 offset=-lbR,
	 title="Regular Bullish Label",
	 text="R",
	 style=shape.labelup,
	 location=location.absolute,
	 color=hasBullishDiv ? color.new(bullColor, 0) : na,
	 textcolor=textColor
	 )

//-----------------------
// Hidden Bullish
//-----------------------

plot(
	 pivotLowFound ? _rsi[lbR] : na,
	 offset=-lbR,
	 title="Hidden Bullish",
	 linewidth=divergenceLineWidth,
	 color=hasHiddenBullishDiv ? color.new(hiddenBullColor, divergenceLineTransparency) : na
	 )

plotshape(
	 hasHiddenBullishDiv ? _rsi[lbR] : na,
	 offset=-lbR,
	 title="Hidden Bullish Label",
	 text="H",
	 location=location.absolute,
	 style=shape.labelup,
	 color=color.new(bearColor, 0),
	 textcolor=textColor
	 )

//-----------------------
// Regular Bearish
//-----------------------

plot(
	 pivotHighFound ? _rsi[lbR] : na,
	 offset=-lbR,
	 title="Regular Bearish",
	 linewidth=divergenceLineWidth,
	 color=hasBearishDiv ? color.new(bearColor, divergenceLineTransparency) : na
	 )

plotshape(
	 hasBearishDiv ? _rsi[lbR] : na,
	 offset=-lbR,
	 title="Regular Bearish Label",
	 text="R",
	 location=location.absolute,
	 style=shape.labeldown,
	 color=color.new(bearColor, 0),
	 textcolor=textColor
	 )

//-----------------------
// Hidden Bearish
//-----------------------

plot(
	 pivotHighFound ? _rsi[lbR] : na,
	 offset=-lbR,
	 title="Hidden Bearish",
	 linewidth=divergenceLineWidth,
	 color=hasHiddenBearishDiv ? color.new(hiddenBearColor, divergenceLineTransparency) : na
	 )

plotshape(
	 hasHiddenBearishDiv ? _rsi[lbR] : na,
	 offset=-lbR,
	 title="Hidden Bearish Label",
	 text="H",
	 location=location.absolute,
	 style=shape.labeldown,
	 color=color.new(bearColor, 0),
	 textcolor=textColor
	 )
	 
// #############################################################################
// BG COlOR (when outside bands)
// #############################################################################

belowLowerBand = slowMA < lowerBand
aboveUpperBand = slowMA > upperBand
bgColor = color.new(coloredBgWhenOutsideBands ? (belowLowerBand ? color.purple : aboveUpperBand ? color.blue : na) : na, bgColorTransparency)
bgcolor(bgColor)
