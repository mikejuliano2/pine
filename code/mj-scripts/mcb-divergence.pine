//@version=4
// Written by Adler from the Market Cipher Discord
// This is meant to overlay Market Cipher B by WVC: https://www.tradingview.com/script/KOnEVsCJ-MarketCipher-B/
// Divergence detection is based off yatrader2's script here and used with his permission (huge thanks to him!): https://www.tradingview.com/script/sxZRzQzQ-Divergence-Indicator-any-oscillator/
study(title="MCB - Divergence", format=format.price, overlay=true)

// === How to use this ===
// 1) Add this indicator to your charts TWICE. Move one of them into the same pane as Market Cipher B, and leave the other in the window with your candles.
// 2) In both of the indicators, click "Settings" and set the indicator input dropdown to MC-B's Blue Wave.
// 3) In the indicator overlaying your candles, check the "Check this box if this indicator is overlaying your candles" box.
// 4) In both of the indicators, click "..." for this tool -> "Visual Order" -> "Bring to Front" so they're easier to see.


// set divergence line to dotted
// stack labels

// ============================================
// INPUTS
// ============================================

// source inputs
blueWave = input(hlc3, title="Set this to MC-B Blue Wave")
isOverlayingCandles = input(false, title="Check this box if this indicator is overlaying your candles")

// config options
showRegularDiv = input(true)
showHiddenDiv = input(false)
bullColorOne = input(#ffa726, title="bullColorOne")
bullColorTwo = input(#000000, title="bullColorTwo")
bearColorOne = input(#ffa726, title="bearColorOne")
bearColorTwo = input(#000000, title="bearColorTwo")
divLineTransparency = input(0, 'divLineTransparency', minval=0, maxval=100)
divShapeTransparency = input(10, 'divShapeTransparency', minval=0, maxval=100)
divLineWidth = input(3, 'divLineWidth', minval=1, maxval=4)
divLabelHiddenBear = 'H'
divLabelHiddenBull = 'H'
divLabelRegularBear = 'R'
divLabelRegularBull = 'R'

// ============================================
// DIVERGENCES (Price vs Blue Wave)
// ============================================

// Divergence detection is based off yatrader2's script here and used with his permission (huge thanks to him!): https://www.tradingview.com/script/sxZRzQzQ-Divergence-Indicator-any-oscillator/

lookbackRight = input(title="DIVERGENCES: Pivot Lookback Right", defval=1)
lookbackLeft = input(title="DIVERGENCES: Pivot Lookback Left", defval=5)
rangeUpper = input(title="DIVERGENCES: Max of Lookback Range", defval=60)
rangeLower = input(title="DIVERGENCES: Min of Lookback Range", defval=5)

plFound = na(pivotlow(blueWave, lookbackLeft, lookbackRight)) ? false : true
phFound = na(pivothigh(blueWave, lookbackLeft, lookbackRight)) ? false : true

_inRange(cond) =>
	bars = barssince(cond == true)
	rangeLower <= bars and bars <= rangeUpper

plFound and

blueWaveHasHigherLow = blueWave[lookbackRight] > valuewhen(plFound, blueWave[lookbackRight], 1) and _inRange(plFound[1])
priceHasLowerLow = low[lookbackRight] < valuewhen(plFound, low[lookbackRight], 1)
hasRegularBullishDiv = priceHasLowerLow and blueWaveHasHigherLow and plFound

blueWaveHasLowerLow = blueWave[lookbackRight] < valuewhen(plFound, blueWave[lookbackRight], 1) and _inRange(plFound[1])
priceHasHigherLow = low[lookbackRight] > valuewhen(plFound, low[lookbackRight], 1)
hasHiddenBullishDiv = priceHasHigherLow and blueWaveHasLowerLow and plFound

blueWaveHasLowerHigh = blueWave[lookbackRight] < valuewhen(phFound, blueWave[lookbackRight], 1) and _inRange(phFound[1])
priceHasHigherHigh = high[lookbackRight] > valuewhen(phFound, high[lookbackRight], 1)
hasRegularBearishDiv = priceHasHigherHigh and blueWaveHasLowerHigh and phFound

blueWaveHasHigherHigh = blueWave[lookbackRight] > valuewhen(phFound, blueWave[lookbackRight], 1) and _inRange(phFound[1])
priceHasLowerHigh = high[lookbackRight] < valuewhen(phFound, high[lookbackRight], 1)
hasHiddenBearishDiv = priceHasLowerHigh and blueWaveHasHigherHigh and phFound




plot(
     showRegularDiv and plFound ? isOverlayingCandles ? low[lookbackRight] : blueWave[lookbackRight] : na,
     offset=-lookbackRight,
     title="Regular Bullish Divergence",
     linewidth=divLineWidth,
     color=hasRegularBullishDiv ? bullColorOne : color.new(color.white, 100)
     )

plotshape(
	 showRegularDiv and hasRegularBullishDiv ? isOverlayingCandles ? low[lookbackRight] : blueWave[lookbackRight] : na,
	 offset=-lookbackRight,
	 title="Regular Bullish Label",
	 text=divLabelRegularBull,
	 style=shape.labelup,
	 location=location.absolute,
	 color=color.new(bullColorOne, divShapeTransparency),
	 textcolor=bullColorTwo
	 )

plot(
	 showHiddenDiv and plFound ? isOverlayingCandles ? low[lookbackRight] : blueWave[lookbackRight] : na,
	 offset=-lookbackRight,
	 title="Hidden Bullish",
	 linewidth=divLineWidth,
	 color=(hasHiddenBullishDiv ? bullColorOne : color.new(color.white, 100))
	 )

plotshape(
	 showHiddenDiv and hasHiddenBullishDiv ? isOverlayingCandles ? low[lookbackRight] : blueWave[lookbackRight] : na,
	 offset=-lookbackRight,
	 title="Hidden Bullish Label",
	 text=divLabelHiddenBull,
	 style=shape.labelup,
	 location=location.absolute,
	 color=bullColorOne,
	 textcolor=bullColorTwo
	 )

plot(
	 showRegularDiv and phFound ? isOverlayingCandles ? high[lookbackRight] : blueWave[lookbackRight] : na,
	 offset=-lookbackRight,
	 title="Regular Bearish",
	 linewidth=divLineWidth,
	 color=(hasRegularBearishDiv ? bearColorOne : color.new(color.white, 100))
	 )

plotshape(
	 showRegularDiv and hasRegularBearishDiv ? isOverlayingCandles ? high[lookbackRight] : blueWave[lookbackRight] : na,
	 offset=-lookbackRight,
	 title="Regular Bearish Label",
	 text=divLabelRegularBear,
	 style=shape.labeldown,
	 location=location.absolute,
	 color=bearColorOne,
	 textcolor=bearColorTwo
	 )

plot(
	 showHiddenDiv and phFound ? isOverlayingCandles ? high[lookbackRight] : blueWave[lookbackRight] : na,
	 offset=-lookbackRight,
	 title="Hidden Bearish",
	 linewidth=divLineWidth,
	 color=(hasHiddenBearishDiv ? bearColorOne : color.new(color.white, 100))
	 )

plotshape(
     showHiddenDiv and hasHiddenBearishDiv ? isOverlayingCandles ? high[lookbackRight] : blueWave[lookbackRight] : na,
	 offset=-lookbackRight,
	 title="Hidden Bearish Label",
	 text=divLabelHiddenBear,
	 style=shape.labeldown,
	 location=location.absolute,
	 color=bearColorOne,
	 textcolor=bearColorTwo
	 )

// -------------------------------
// DIVERGENCE ALERTS
// -------------------------------

alertcondition(hasRegularBullishDiv, "Regular Bullish Divergence", "Regular Bullish Divergence")
alertcondition(hasHiddenBullishDiv, "Hidden Bullish Divergence", "Hidden Bullish Divergence")
alertcondition(hasRegularBearishDiv, "Regular Bearish Divergence", "Regular Bearish Divergence")
alertcondition(hasHiddenBearishDiv, "Hidden Bearish Divergence", "Hidden Bearish Divergence")
